buildscript {
  repositories {
    mavenCentral()
  }
  dependencies { classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.10' }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.google.protobuf'

description = 'GRPC library for the servicecontrol-v1 service'
// TODO: use a flag to determine whether to produce a release or a snapshot
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  compile platform(libraries.protobufJavaBom),
          libraries.protobufJava

  compile platform(libraries.grpcBom),
          libraries.grpcStub,
          libraries.grpcProtobuf
}

protobuf {
  protoc {
    // The version of protoc must match protobuf-java. If you don't depend on
    // protobuf-java directly, you will be transitively depending on the
    // protobuf-java version that grpc depends on.
    artifact = "com.google.protobuf:protoc:${protobufJavaVersion}"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {
        outputSubDir = 'java'
      }
    }
  }
}

sourceSets {
  main {
    java {
      srcDir 'build/generated/source/proto/main/java'
    }
    proto {
      srcDir 'src/auxi/proto'
    }
  }
}
sourceSets.main.proto.setIncludes([
  'google/api/servicecontrol/v1/*.proto',
  'google/api/experimental/*.proto'])

configureMaven(
  project,
  'Endpoints Management - Generated Java Protobufs',
  'Contains the generated protocol buffer surface used by Endpoints'
)
archivesBaseName = 'endpoints-management-protos'
